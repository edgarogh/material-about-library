apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = 'com.github.daniel-stoneuk'

def versionMajor = 3
def versionMinor = 1
def versionPatch = 2
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

def versionFullName = "${versionMajor}.${versionMinor}" + (versionPatch == 0 ? "" : ".${versionPatch}")

android {
    namespace "com.danielstone.materialaboutlibrary"
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName versionFullName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = group
            artifactId = 'materialaboutlibrary'
            version = versionFullName

            afterEvaluate {
                from components.release
            }
        }
    }
}
